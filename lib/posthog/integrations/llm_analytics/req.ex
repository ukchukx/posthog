defmodule PostHog.Integrations.LLMAnalytics.Req do
  @moduledoc since: "2.2.0"
  @moduledoc """
  Req plugin that automatically captures
  [`$ai_generation`](https://posthog.com/docs/llm-analytics/manual-capture?tab=Generation)
  events for LLMs.

  It tries to extract as much information as possible from both requests and
  responses. Currently, it works best with the following APIs:
  * OpenAI (Responses)
  * OpenAI (Chat Completions)
  * Anthropic (Create Message)
  * Gemini (generateContent)

  ## Usage

  Just add it to your `Req` client before making a call:

  ```
  Req.new()
  |> PostHog.Integrations.LLMAnalytics.Req.attach()
  |> Req.post!(url: "https://api.openai.com/v1/responses", json: %{model: "gpt-5-mini", input: "Who are you?"})
  ```

  Optionally, start a new span beforehand to add additional properties to the event:

  ```
  PostHog.LLMAnalytics.start_span(%{"$ai_span_name": "OpenAI Request"})
  Req.post!(client, url: "https://api.openai.com/v1/responses", json: ...)
  ```

  ## Integrating with InstructorLite

  InstructorLite built-in adapters allow customizing the HTTP client using the
  [`http_client`
  option](https://hexdocs.pm/instructor_lite/InstructorLite.Adapters.OpenAI.html#send_request/2).
  Define a wrapper module like this:

  ```
  defmodule ReqWithLLMAnalytics do
    def post(url, opts) do
      Req.new(url: url)
      |> PostHog.Integrations.LLMAnalytics.Req.attach()
      |> Req.post(opts)
    end
  end
  ```

  Then pass this module as the `http_client` option in `adapter_context`.
  Optionally, start a span beforehand!

  ```
  PostHog.LLMAnalytics.start_span(%{"$ai_span_name": "LLM Call"})

  InstructorLite.instruct(%{
      input: [%{role: "user", content: "John is 25yo"}],
      model: "gpt-4o-mini"
    },
    response_model: %{name: :string, age: :integer},
    adapter: InstructorLite.Adapters.OpenAI,
    adapter_context: [
      api_key: "my-secret-key",
      http_client: ReqWithLLMAnalytics
    ]
  )
  {:ok, %{name: "John", age: 25}}
  ```
  """
  @start_at_key :posthog_llm_analytics_start_at
  @properties_key :posthog_llm_analytics_properties

  alias PostHog.LLMAnalytics

  @doc """
  Attaches the LLM Analytics plugin to a `Req.Request` struct.

  ## Parameters

  - `request` - the `Req.Request` to instrument.
  - `options` - plugin options merged into the request.

  ## Options

  - `:posthog_supervisor` - PostHog supervisor name to capture through. Use this
    if you run a [custom PostHog instance](https://posthog.com/docs/libraries/elixir#advanced-configuration).

  ## Returns

  Returns the updated `Req.Request` with request, response, and error steps
  installed.

  ## Examples

      iex> Req.new() |> PostHog.Integrations.LLMAnalytics.Req.attach()
      iex> Req.new() |> PostHog.Integrations.LLMAnalytics.Req.attach(posthog_supervisor: MyPostHog)
  """
  @spec attach(Req.Request.t(), keyword()) :: Req.Request.t()
  def attach(%Req.Request{} = request, options \\ []) do
    request
    |> Req.Request.register_options([:posthog_supervisor])
    |> Req.Request.merge_options(options)
    |> Req.Request.append_request_steps(
      posthog_llm_analytics_request_properties: &put_request_properties/1,
      posthog_llm_analytics_latency_start: &put_start_time/1
    )
    |> Req.Request.prepend_response_steps(posthog_llm_analytics_latency_stop: &put_latency/1)
    |> Req.Request.prepend_error_steps(
      posthog_llm_analytics_latency_stop: &put_latency/1,
      posthog_llm_analytics_error_properties: &put_error_properties/1
    )
    |> Req.Request.append_error_steps(
      posthog_llm_analytics_capture_generation: &capture_generation/1
    )
    |> Req.Request.append_response_steps(
      posthog_llm_analytics_response_properties: &put_response_properties/1,
      posthog_llm_analytics_capture_generation: &capture_generation/1
    )
  end

  defp put_start_time(request) do
    Req.Request.put_private(request, @start_at_key, System.monotonic_time())
  end

  defp put_latency({request, response}) do
    stop_time = System.monotonic_time()
    start_time = Req.Request.get_private(request, @start_at_key)
    latency = System.convert_time_unit(stop_time - start_time, :native, :millisecond) / 1000
    request = put_properties(request, %{"$ai_latency": latency})
    {request, response}
  end

  defp put_request_properties(request) do
    request
    |> put_properties(request_url_properties(request))
    |> put_properties(request_body_properties(request))
  end

  defp put_response_properties({request, response}) do
    properties =
      response.body
      |> response_properties()
      |> Map.put(:"$ai_http_status", response.status)

    {put_properties(request, properties), response}
  end

  defp put_error_properties({request, exception}) when is_exception(exception) do
    request =
      put_properties(request, %{"$ai_is_error": true, "$ai_error": Exception.message(exception)})

    {request, exception}
  end

  defp put_error_properties({request, exception}) do
    request = put_properties(request, %{"$ai_is_error": true, "$ai_error": inspect(exception)})
    {request, exception}
  end

  defp put_properties(request, properties) do
    Req.Request.update_private(request, @properties_key, properties, fn current ->
      Map.merge(current, properties)
    end)
  end

  defp capture_generation({request, response_or_exception}) do
    properties = Req.Request.get_private(request, @properties_key, %{})

    LLMAnalytics.capture_current_span(
      request.options[:posthog_supervisor] || PostHog,
      "$ai_generation",
      properties
    )

    {request, response_or_exception}
  end

  defp request_url_properties(
         %Req.Request{url: %URI{host: "api.openai.com", path: "/v1" <> _}} = request
       ) do
    %{
      "$ai_base_url": "https://api.openai.com/v1",
      "$ai_request_url": URI.to_string(request.url),
      "$ai_provider": "openai"
    }
  end

  defp request_url_properties(
         %Req.Request{
           url: %URI{host: "generativelanguage.googleapis.com", path: "/v1beta/models" <> _}
         } = request
       ) do
    properties =
      with true <- is_binary(request.url.query),
           %{"alt" => "sse"} <- URI.decode_query(request.url.query) do
        %{"$ai_stream": true}
      else
        _ -> %{}
      end

    Map.merge(properties, %{
      "$ai_base_url": "https://generativelanguage.googleapis.com/v1beta/models",
      "$ai_request_url": URI.to_string(request.url),
      "$ai_provider": "gemini"
    })
  end

  defp request_url_properties(
         %Req.Request{
           url: %URI{
             host: "generativelanguage.googleapis.com",
             path: "/v1beta/openai/chat/completions"
           }
         } = request
       ) do
    %{
      "$ai_base_url": "https://generativelanguage.googleapis.com/v1beta/openai",
      "$ai_request_url": URI.to_string(request.url),
      "$ai_provider": "gemini"
    }
  end

  defp request_url_properties(
         %Req.Request{url: %URI{host: "api.anthropic.com", path: "/v1/messages" <> _}} = request
       ) do
    %{
      "$ai_base_url": "https://api.anthropic.com/v1/messages",
      "$ai_request_url": URI.to_string(request.url),
      "$ai_provider": "anthropic"
    }
  end

  defp request_url_properties(%Req.Request{} = request) do
    %{
      "$ai_base_url": URI.to_string(%{request.url | path: nil}),
      "$ai_request_url": URI.to_string(request.url)
    }
  end

  defp request_url_properties(_), do: %{}

  defp request_body_properties(%Req.Request{options: %{json: json_body}}) do
    Enum.reduce(
      [:"$ai_input", :"$ai_temperature", :"$ai_stream", :"$ai_max_tokens", :"$ai_tools"],
      %{},
      fn property, properties ->
        if value = request_optional_property(property, json_body) do
          Map.put(properties, property, value)
        else
          properties
        end
      end
    )
  end

  defp request_body_properties(_), do: %{}

  defp request_optional_property(:"$ai_input", body) do
    # OpenAI Responses
    # OpenAI Chat Completions
    # Gemini generateContent
    # Anthropic
    get_in(body, [atom_or_string_key(:input)]) ||
      get_in(body, [atom_or_string_key(:messages)]) ||
      get_in(body, [atom_or_string_key(:contents)])
  end

  defp request_optional_property(:"$ai_temperature", body) do
    # OpenAI Responses
    # OpenAI Chat Completions
    # Anthropic
    get_in(body, [atom_or_string_key(:temperature)]) ||
      get_in(body, [atom_or_string_key(:generationConfig), atom_or_string_key(:temperature)])
  end

  defp request_optional_property(:"$ai_stream", body) do
    # OpenAI Responses
    # OpenAI Chat Completions
    get_in(body, [atom_or_string_key(:stream)])
  end

  defp request_optional_property(:"$ai_max_tokens", body) do
    # OpenAI Responses
    # OpenAI Chat Completions
    # Anthropic
    get_in(body, [atom_or_string_key(:max_output_tokens)]) ||
      get_in(body, [atom_or_string_key(:max_completion_tokens)]) ||
      get_in(body, [atom_or_string_key(:generationConfig), atom_or_string_key(:maxOutputTokens)]) ||
      get_in(body, [atom_or_string_key(:max_tokens)])
  end

  defp request_optional_property(:"$ai_tools", body) do
    # OpenAI Responses
    # OpenAI Chat Completions
    # Gemini
    # Anthropic
    get_in(body, [atom_or_string_key(:tools)])
  end

  defp request_optional_property(_, _), do: nil

  # OpenAI Responses
  defp response_properties(%{
         "model" => model,
         "output" => output,
         "usage" => %{"output_tokens" => output_tokens, "input_tokens" => input_tokens},
         "tools" => tools,
         "temperature" => temperature
       }) do
    %{
      "$ai_output_choices": output,
      "$ai_input_tokens": input_tokens,
      "$ai_output_tokens": output_tokens,
      "$ai_model": model,
      "$ai_tools": tools,
      "$ai_temperature": temperature,
      "$ai_is_error": false
    }
  end

  # OpenAI Chat Completions
  defp response_properties(%{
         "model" => model,
         "choices" => output,
         "usage" => %{"completion_tokens" => output_tokens, "prompt_tokens" => input_tokens}
       }) do
    %{
      "$ai_output_choices": output,
      "$ai_input_tokens": input_tokens,
      "$ai_output_tokens": output_tokens,
      "$ai_model": model,
      "$ai_is_error": false
    }
  end

  # Gemini generateContent
  defp response_properties(%{
         "candidates" => output,
         "modelVersion" => model,
         "usageMetadata" =>
           %{
             "promptTokenCount" => input_tokens,
             "candidatesTokenCount" => candidates_tokens
           } = usage
       }) do
    reasoning_tokens = usage["thoughtsTokenCount"] || 0
    tool_use_tokens = usage["toolUsePromptTokenCount"] || 0

    %{
      "$ai_output_choices": output,
      "$ai_input_tokens": input_tokens,
      "$ai_output_tokens": candidates_tokens + reasoning_tokens + tool_use_tokens,
      "$ai_model": model,
      "$ai_is_error": false
    }
  end

  # Anthropic Create Message
  defp response_properties(%{
         "model" => model,
         "content" => output,
         "usage" => %{"output_tokens" => output_tokens, "input_tokens" => input_tokens}
       }) do
    %{
      "$ai_output_choices": output,
      "$ai_input_tokens": input_tokens,
      "$ai_output_tokens": output_tokens,
      "$ai_model": model,
      "$ai_is_error": false
    }
  end

  defp response_properties(%{"error" => error}) do
    %{
      "$ai_is_error": true,
      "$ai_error": error
    }
  end

  defp response_properties(_), do: %{}

  defp atom_or_string_key(key) do
    fn :get, data, next ->
      value = Access.get(data, key) || Access.get(data, Atom.to_string(key))

      if value do
        next.(value)
      else
        nil
      end
    end
  end
end
