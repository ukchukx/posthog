defmodule PostHog.FeatureFlagsTest do
  use PostHog.Case,
    async: Version.match?(System.version(), ">= 1.18.0"),
    group: PostHog

  @moduletag config: [supervisor_name: PostHog]

  import Mox

  alias PostHog.API
  alias PostHog.FeatureFlags
  alias PostHog.FeatureFlags.Result

  setup :setup_supervisor
  setup :verify_on_exit!

  describe "flags/2" do
    test "returns body on success" do
      expect(API.Mock, :request, fn _client, method, url, opts ->
        assert method == :post
        assert url == "/flags"
        assert opts[:params] == %{v: 2}

        assert opts[:json] == %{
                 distinct_id: "foo"
               }

        {:ok, %{status: 200, body: %{"flags" => %{}}}}
      end)

      assert {:ok, %{status: 200, body: %{}}} = FeatureFlags.flags(%{distinct_id: "foo"})
    end

    test "sophisticated body" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{
                 distinct_id: "foo",
                 groups: %{group_type: "group_id"}
               }

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, %{}} =
               FeatureFlags.flags(%{
                 distinct_id: "foo",
                 groups: %{group_type: "group_id"}
               })
    end

    test "client errors passed as is" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:error, :transport_error}
      end)

      assert {:error, :transport_error} = FeatureFlags.flags("foo")
    end

    test "non-200 is wrapped in error" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 503}}
      end)

      assert {:error,
              %PostHog.UnexpectedResponseError{
                response: %{status: 503},
                message: "Unexpected response"
              }} =
               FeatureFlags.flags(%{distinct_id: "foo"})
    end

    test "unexpected response body" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: "internal server error"}}
      end)

      assert {:error,
              %PostHog.UnexpectedResponseError{
                response: "internal server error",
                message: "Expected response body to have \"flags\" key"
              }} = FeatureFlags.flags(%{distinct_id: "foo"})
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, %{body: %{"flags" => _}}} =
               FeatureFlags.flags(MyPostHog, %{distinct_id: "foo"})
    end

    @tag config: [api_key: "", supervisor_name: PostHog]
    test "returns empty flags when PostHog is disabled" do
      assert {:ok, %{status: 200, body: %{"flags" => %{}}}} =
               FeatureFlags.flags(%{distinct_id: "foo"})
    end
  end

  describe "flags_for/2" do
    test "returns flags on success" do
      expect(API.Mock, :request, fn _client, method, url, opts ->
        assert method == :post
        assert url == "/flags"
        assert opts[:params] == %{v: 2}

        assert opts[:json] == %{
                 distinct_id: "foo"
               }

        {:ok, %{status: 200, body: %{"flags" => %{"foo" => %{}}}}}
      end)

      assert {:ok, %{"foo" => %{}}} = FeatureFlags.flags_for("foo")
    end

    test "full request map" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo", personal_properties: %{foo: "bar"}}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, %{}} =
               FeatureFlags.flags_for(%{
                 distinct_id: "foo",
                 personal_properties: %{foo: "bar"}
               })
    end

    test "distinct_id is taken from the context if not passed" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, %{}} = FeatureFlags.flags_for()
    end

    test "explicit distinct_id preferred over context" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "bar"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, %{}} = FeatureFlags.flags_for("bar")
    end

    test "missing distinct_Id" do
      assert {:error,
              %PostHog.Error{
                message:
                  "distinct_id is required but wasn't explicitly provided or found in the context"
              }} =
               FeatureFlags.flags_for(nil)
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, %{"example-feature-flag-1" => %{}}} =
               FeatureFlags.flags_for(MyPostHog, "foo")
    end
  end

  describe "check/3" do
    test "returns variant if present" do
      expect(API.Mock, :request, fn _client, method, url, opts ->
        assert method == :post
        assert url == "/flags"
        assert opts[:params] == %{v: 2}

        assert opts[:json] == %{
                 distinct_id: "foo"
               }

        {:ok,
         %{
           status: 200,
           body: %{"flags" => %{"myflag" => %{"enabled" => true, "variant" => "variant1"}}}
         }}
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")
    end

    test "returns true if enabled" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{"enabled" => true}}}}}
      end)

      assert {:ok, true} = FeatureFlags.check("myflag", "foo")
    end

    test "returns false otherwise" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{}}}}}
      end)

      assert {:ok, false} = FeatureFlags.check("myflag", "foo")
    end

    test "full request map" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo", personal_properties: %{foo: "bar"}}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, true} =
               FeatureFlags.check("example-feature-flag-1", %{
                 distinct_id: "foo",
                 personal_properties: %{foo: "bar"}
               })
    end

    test "distinct_id is taken from the context if not passed" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, true} = FeatureFlags.check("example-feature-flag-1")
    end

    test "explicit distinct_id preferred over context" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "bar"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, true} = FeatureFlags.check("example-feature-flag-1", "bar")
    end

    test "missing distinct_Id" do
      assert {:error,
              %PostHog.Error{
                message:
                  "distinct_id is required but wasn't explicitly provided or found in the context"
              }} =
               FeatureFlags.check("example-feature-flag-1")
    end

    test "sets feature flag context" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{"variant" => "variant1"}}}}}
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")
      assert %{"$feature/myflag" => "variant1"} = PostHog.get_context()
    end

    test "publishes $feature_flag_called event " do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{"variant" => "variant1"}}}}}
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 distinct_id: "foo",
                 properties: %{"$feature_flag": "myflag", "$feature_flag_response": "variant1"}
               }
             ] = all_captured()
    end

    test "includes evaluatedAt in $feature_flag_called event when present" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{"myflag" => %{"variant" => "variant1"}},
             "evaluatedAt" => 1_234_567_890
           }
         }}
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 distinct_id: "foo",
                 properties: %{
                   "$feature_flag": "myflag",
                   "$feature_flag_response": "variant1",
                   "$feature_flag_evaluated_at": 1_234_567_890
                 }
               }
             ] = all_captured()
    end

    test "attaches $feature_flag_error: errors_while_computing_flags when response signals errors" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{"myflag" => %{"enabled" => true, "variant" => "variant1"}},
             "errorsWhileComputingFlags" => true
           }
         }}
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 properties: %{
                   "$feature_flag": "myflag",
                   "$feature_flag_error": "errors_while_computing_flags"
                 }
               }
             ] = all_captured()
    end

    test "attaches rich metadata to $feature_flag_called when the response provides it" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{
                   "id" => 42,
                   "version" => 7,
                   "payload" => nil,
                   "has_experiment" => true
                 },
                 "reason" => %{"code" => "condition_match"}
               }
             },
             "requestId" => "req-xyz",
             "evaluatedAt" => 1_700_000_000
           }
         }}
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 distinct_id: "foo",
                 properties: %{
                   "$feature_flag": "myflag",
                   "$feature_flag_response": "variant1",
                   "$feature_flag_id": 42,
                   "$feature_flag_version": 7,
                   "$feature_flag_reason": %{"code" => "condition_match"},
                   "$feature_flag_request_id": "req-xyz",
                   "$feature_flag_evaluated_at": 1_700_000_000,
                   "$feature_flag_has_experiment": true
                 }
               }
             ] = all_captured()
    end

    test "sets $feature_flag_has_experiment: false when the response reports it as false" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"id" => 42, "version" => 7, "has_experiment" => false}
               }
             }
           }
         }}
      end)

      assert {:ok, true} = FeatureFlags.check("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 properties: %{"$feature_flag_has_experiment": false}
               }
             ] = all_captured()
    end

    test "omits $feature_flag_has_experiment when the response omits has_experiment" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"id" => 42, "version" => 7}
               }
             }
           }
         }}
      end)

      assert {:ok, true} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", properties: properties}] = all_captured()
      refute Map.has_key?(properties, :"$feature_flag_has_experiment")
    end

    test "omits $feature_flag_has_experiment when the response reports a non-boolean value" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"id" => 42, "has_experiment" => "yes"}
               }
             }
           }
         }}
      end)

      assert {:ok, true} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", properties: properties}] = all_captured()
      refute Map.has_key?(properties, :"$feature_flag_has_experiment")
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert {:ok, true} = FeatureFlags.check(MyPostHog, "example-feature-flag-1", "foo")
    end
  end

  describe "minimal $feature_flag_called events" do
    defp minimal_gated_response(overrides) do
      flag =
        Map.merge(
          %{
            "enabled" => true,
            "variant" => "variant1",
            "metadata" => %{
              "id" => 42,
              "version" => 7,
              "payload" => ~s({"copy": "hi"}),
              "has_experiment" => false
            },
            "reason" => %{"code" => "condition_match"}
          },
          Map.get(overrides, "flag", %{})
        )

      body =
        Map.merge(
          %{
            "flags" => %{"myflag" => flag},
            "requestId" => "req-xyz",
            "evaluatedAt" => 1_700_000_000,
            "minimalFlagCalledEvents" => true
          },
          Map.delete(overrides, "flag")
        )

      {:ok, %{status: 200, body: body}}
    end

    @tag config: [supervisor_name: PostHog, global_properties: %{team: "growth"}]
    test "sends exactly the allowlisted properties when gated and the flag has no experiment" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        minimal_gated_response(%{"errorsWhileComputingFlags" => true})
      end)

      PostHog.set_context(%{
        some_tag: "ctx",
        "$feature/other-flag": true,
        "$groups": %{company: "acme"},
        "$process_person_profile": false
      })

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", distinct_id: "foo", properties: properties}] =
               all_captured()

      assert properties == %{
               "$feature_flag": "myflag",
               "$feature_flag_response": "variant1",
               "$feature_flag_has_experiment": false,
               "$feature_flag_id": 42,
               "$feature_flag_version": 7,
               "$feature_flag_reason": %{"code" => "condition_match"},
               "$feature_flag_request_id": "req-xyz",
               "$feature_flag_evaluated_at": 1_700_000_000,
               "$feature_flag_error": "errors_while_computing_flags",
               "$groups": %{company: "acme"},
               "$process_person_profile": false,
               "$lib": "posthog-elixir",
               "$lib_version": PostHog.Lib.version(),
               "$is_server": true
             }
    end

    test "keeps the session ID set by the Plug integration and strips the rest of the request context" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        minimal_gated_response(%{})
      end)

      :get
      |> Plug.Test.conn("https://posthog.com/foo?bar=10")
      |> Plug.Conn.put_req_header("x-posthog-session-id", "session-123")
      |> Plug.Conn.put_req_header("user-agent", "Mozilla/5.0")
      |> PostHog.Integrations.Plug.call(nil)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", distinct_id: "foo", properties: properties}] =
               all_captured()

      assert properties == %{
               "$feature_flag": "myflag",
               "$feature_flag_response": "variant1",
               "$feature_flag_has_experiment": false,
               "$feature_flag_id": 42,
               "$feature_flag_version": 7,
               "$feature_flag_reason": %{"code" => "condition_match"},
               "$feature_flag_request_id": "req-xyz",
               "$feature_flag_evaluated_at": 1_700_000_000,
               "$session_id": "session-123",
               "$lib": "posthog-elixir",
               "$lib_version": PostHog.Lib.version(),
               "$is_server": true
             }
    end

    test "keeps string-keyed allowlisted context properties on minimal events" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        minimal_gated_response(%{})
      end)

      PostHog.set_context(%{
        "some_tag" => "ctx",
        "$groups" => %{"company" => "acme"},
        "$process_person_profile" => false
      })

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", properties: properties}] = all_captured()
      assert properties["$groups"] == %{"company" => "acme"}
      assert properties["$process_person_profile"] == false
      refute Map.has_key?(properties, "some_tag")
      refute Map.has_key?(properties, "$feature/myflag")
    end

    test "sends the full event when gated but the flag has an experiment" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        minimal_gated_response(%{"flag" => %{"metadata" => %{"has_experiment" => true}}})
      end)

      PostHog.set_context(%{some_tag: "ctx"})

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", properties: properties}] = all_captured()
      assert properties["$feature/myflag"] == "variant1"
      assert properties[:"$feature_flag_has_experiment"] == true
      assert properties[:some_tag] == "ctx"
      assert properties[:"$is_server"] == true
    end

    test "sends the full event when the gate is absent, false, or unparseable" do
      for {gate_overrides, distinct_id} <- [
            {%{"minimalFlagCalledEvents" => nil}, "user-absent"},
            {%{"minimalFlagCalledEvents" => false}, "user-false"},
            {%{"minimalFlagCalledEvents" => "yes"}, "user-unparseable"}
          ] do
        expect(API.Mock, :request, fn _client, _method, _url, _opts ->
          minimal_gated_response(gate_overrides)
        end)

        assert {:ok, "variant1"} = FeatureFlags.check("myflag", distinct_id)
      end

      events = all_captured()
      assert length(events) == 3

      for %{event: "$feature_flag_called", properties: properties} <- events do
        assert properties["$feature/myflag"] == "variant1"
        assert properties[:"$is_server"] == true
      end
    end

    test "sends the full event when gated but has_experiment is missing" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        minimal_gated_response(%{"flag" => %{"metadata" => %{"id" => 42, "version" => 7}}})
      end)

      assert {:ok, "variant1"} = FeatureFlags.check("myflag", "foo")

      assert [%{event: "$feature_flag_called", properties: properties}] = all_captured()
      assert properties["$feature/myflag"] == "variant1"
      refute Map.has_key?(properties, :"$feature_flag_has_experiment")
    end
  end

  describe "get_feature_flag_result/4" do
    test "returns Result struct for boolean flag" do
      expect(API.Mock, :request, fn _client, method, url, opts ->
        assert method == :post
        assert url == "/flags"
        assert opts[:params] == %{v: 2}

        assert opts[:json] == %{
                 distinct_id: "foo"
               }

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok,
              %Result{
                key: "myflag",
                enabled: true,
                variant: nil,
                payload: nil
              }} = FeatureFlags.get_feature_flag_result("myflag", "foo")
    end

    test "returns FeatureFlagResult struct for variant flag with payload" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => %{"key" => "value"}}
               }
             }
           }
         }}
      end)

      assert {:ok,
              %Result{
                key: "myflag",
                enabled: true,
                variant: "variant1",
                payload: %{"key" => "value"}
              }} = FeatureFlags.get_feature_flag_result("myflag", "foo")
    end

    test "returns disabled result when flag not enabled" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => false,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok,
              %Result{
                key: "myflag",
                enabled: false,
                variant: nil,
                payload: nil
              }} = FeatureFlags.get_feature_flag_result("myflag", "foo")
    end

    test "full request map" do
      expect(API.Mock, :request, fn _client, _method, _url, opts ->
        assert opts[:json] == %{distinct_id: "foo", personal_properties: %{foo: "bar"}}

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{key: "myflag", enabled: true}} =
               FeatureFlags.get_feature_flag_result("myflag", %{
                 distinct_id: "foo",
                 personal_properties: %{foo: "bar"}
               })
    end

    test "distinct_id is taken from the context if not passed" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn _client, _method, _url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{key: "myflag", enabled: true}} =
               FeatureFlags.get_feature_flag_result("myflag")
    end

    test "explicit distinct_id preferred over context" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn _client, _method, _url, opts ->
        assert opts[:json] == %{distinct_id: "bar"}

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{key: "myflag", enabled: true}} =
               FeatureFlags.get_feature_flag_result("myflag", "bar")
    end

    test "missing distinct_id" do
      assert {:error,
              %PostHog.Error{
                message:
                  "distinct_id is required but wasn't explicitly provided or found in the context"
              }} =
               FeatureFlags.get_feature_flag_result("myflag")
    end

    test "flag not found returns {:ok, nil}" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{}}}}
      end)

      assert {:ok, nil} = FeatureFlags.get_feature_flag_result("myflag", "foo")
    end

    test "sets feature flag context" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => %{"key" => "value"}}
               }
             }
           }
         }}
      end)

      assert {:ok,
              %Result{
                key: "myflag",
                enabled: true,
                variant: "variant1",
                payload: %{"key" => "value"}
              }} = FeatureFlags.get_feature_flag_result("myflag", "foo")

      assert %{"$feature/myflag" => "variant1"} = PostHog.get_context()
    end

    test "publishes $feature_flag_called event" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => %{"key" => "value"}}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{variant: "variant1"}} =
               FeatureFlags.get_feature_flag_result("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 distinct_id: "foo",
                 properties: %{"$feature_flag": "myflag", "$feature_flag_response": "variant1"}
               }
             ] = all_captured()
    end

    test "includes evaluatedAt in $feature_flag_called event when present" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => nil}
               }
             },
             "evaluatedAt" => 1_234_567_890
           }
         }}
      end)

      assert {:ok, %Result{variant: "variant1"}} =
               FeatureFlags.get_feature_flag_result("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 distinct_id: "foo",
                 properties: %{
                   "$feature_flag": "myflag",
                   "$feature_flag_response": "variant1",
                   "$feature_flag_evaluated_at": 1_234_567_890
                 }
               }
             ] = all_captured()
    end

    test "send_event: false does not publish event or set context" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{variant: "variant1"}} =
               FeatureFlags.get_feature_flag_result("myflag", "foo", send_event: false)

      assert [] = all_captured()
      refute Map.has_key?(PostHog.get_context(), "$feature/myflag")
    end

    test "send_event: true explicitly sends event (default behavior)" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{enabled: true}} =
               FeatureFlags.get_feature_flag_result("myflag", "foo", send_event: true)

      assert [%{event: "$feature_flag_called"}] = all_captured()
    end

    test "does not send event when flag not found" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{}}}}
      end)

      assert {:ok, nil} = FeatureFlags.get_feature_flag_result("myflag", "foo")

      assert [] = all_captured()
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance" do
      expect(API.Mock, :request, fn _client, _method, _url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{key: "myflag", enabled: true}} =
               FeatureFlags.get_feature_flag_result(MyPostHog, "myflag", "foo")
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance with opts" do
      expect(API.Mock, :request, fn _client, _method, _url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert {:ok, %Result{key: "myflag", enabled: true}} =
               FeatureFlags.get_feature_flag_result(MyPostHog, "myflag", "foo", send_event: false)

      assert [] = all_captured()
    end
  end

  describe "get_feature_flag_result!/4" do
    test "returns Result struct for found flag" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => %{"key" => "value"}}
               }
             }
           }
         }}
      end)

      assert %Result{
               key: "myflag",
               enabled: true,
               variant: "variant1",
               payload: %{"key" => "value"}
             } = FeatureFlags.get_feature_flag_result!("myflag", "foo")
    end

    test "returns nil for missing flag (does not raise)" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{}}}}
      end)

      assert nil == FeatureFlags.get_feature_flag_result!("myflag", "foo")
    end

    test "raises on API error (missing distinct_id)" do
      assert_raise PostHog.Error, fn ->
        FeatureFlags.get_feature_flag_result!("myflag")
      end
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance" do
      expect(API.Mock, :request, fn _client, _method, _url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert %Result{key: "myflag", enabled: true} =
               FeatureFlags.get_feature_flag_result!(MyPostHog, "myflag", "foo")
    end

    test "passes through opts (send_event: false)" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok,
         %{
           status: 200,
           body: %{
             "flags" => %{
               "myflag" => %{
                 "enabled" => true,
                 "variant" => "variant1",
                 "metadata" => %{"payload" => nil}
               }
             }
           }
         }}
      end)

      assert %Result{variant: "variant1"} =
               FeatureFlags.get_feature_flag_result!("myflag", "foo", send_event: false)

      assert [] = all_captured()
    end
  end

  describe "check!/3" do
    test "returns variant if present" do
      expect(API.Mock, :request, fn _client, method, url, opts ->
        assert method == :post
        assert url == "/flags"
        assert opts[:params] == %{v: 2}

        assert opts[:json] == %{
                 distinct_id: "foo"
               }

        {:ok,
         %{
           status: 200,
           body: %{"flags" => %{"myflag" => %{"enabled" => true, "variant" => "variant1"}}}
         }}
      end)

      assert "variant1" = FeatureFlags.check!("myflag", "foo")
    end

    test "returns true if enabled" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{"enabled" => true}}}}}
      end)

      assert true = FeatureFlags.check!("myflag", "foo")
    end

    test "returns false otherwise" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{}}}}}
      end)

      assert false == FeatureFlags.check!("myflag", "foo")
    end

    test "full request map" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo", personal_properties: %{foo: "bar"}}

        API.Stub.request(client, method, url, opts)
      end)

      assert true =
               FeatureFlags.check!("example-feature-flag-1", %{
                 distinct_id: "foo",
                 personal_properties: %{foo: "bar"}
               })
    end

    test "distinct_id is taken from the context if not passed" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert true = FeatureFlags.check!("example-feature-flag-1")
    end

    test "explicit distinct_id preferred over context" do
      PostHog.set_context(%{distinct_id: "foo"})

      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "bar"}

        API.Stub.request(client, method, url, opts)
      end)

      assert true = FeatureFlags.check!("example-feature-flag-1", "bar")
    end

    test "missing distinct_id" do
      assert_raise PostHog.Error, fn ->
        FeatureFlags.check!("example-feature-flag-1")
      end
    end

    test "unexpected body shape" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{}}}}
      end)

      assert_raise PostHog.UnexpectedResponseError,
                   "Feature flag example-feature-flag-1 was not found in the response\n\n%{\"flags\" => %{}}",
                   fn ->
                     FeatureFlags.check!("example-feature-flag-1", "bar")
                   end
    end

    test "sets feature flag context" do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{"variant" => "variant1"}}}}}
      end)

      assert "variant1" = FeatureFlags.check!("myflag", "foo")
      assert %{"$feature/myflag" => "variant1"} = PostHog.get_context()
    end

    test "publishes $feature_flag_called event " do
      expect(API.Mock, :request, fn _client, _method, _url, _opts ->
        {:ok, %{status: 200, body: %{"flags" => %{"myflag" => %{"variant" => "variant1"}}}}}
      end)

      assert "variant1" = FeatureFlags.check!("myflag", "foo")

      assert [
               %{
                 event: "$feature_flag_called",
                 distinct_id: "foo",
                 properties: %{"$feature_flag": "myflag", "$feature_flag_response": "variant1"}
               }
             ] = all_captured()
    end

    @tag config: [supervisor_name: MyPostHog]
    test "custom PostHog instance" do
      expect(API.Mock, :request, fn client, method, url, opts ->
        assert opts[:json] == %{distinct_id: "foo"}

        API.Stub.request(client, method, url, opts)
      end)

      assert true = FeatureFlags.check!(MyPostHog, "example-feature-flag-1", "foo")
    end
  end
end
