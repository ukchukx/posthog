defmodule PostHog.API.ClientTest do
  use ExUnit.Case, async: true

  alias PostHog.API.Client

  test "client/2 sets the posthog-elixir User-Agent" do
    %Client{client: req} = Client.client("phc_test", "https://us.i.posthog.com")

    assert req.headers["user-agent"] == [Client.user_agent()]
  end

  for {case_name, response_or_exception, expected} <- [
        {"transport timeout", %Req.TransportError{reason: :timeout}, true},
        {"transport closed", %Req.TransportError{reason: :closed}, true},
        {"contract http status 502", %Req.Response{status: 502}, true},
        {"contract http status 504", %Req.Response{status: 504}, true},
        {"http error", %Req.HTTPError{reason: :closed}, false},
        {"http status 408", %Req.Response{status: 408}, false},
        {"http status 429", %Req.Response{status: 429}, false},
        {"http status 500", %Req.Response{status: 500}, false},
        {"http status 503", %Req.Response{status: 503}, false}
      ] do
    test "flags retry policy handles #{case_name}" do
      assert Client.retry_flags_request?(
               %Req.Request{},
               unquote(Macro.escape(response_or_exception))
             ) == unquote(expected)
    end
  end

  for {retry_count, expected_delay} <- [{0, 300}, {1, 600}, {2, 1200}] do
    test "flags retry delay for retry count #{retry_count}" do
      assert Client.flags_retry_delay(unquote(retry_count)) == unquote(expected_delay)
    end
  end

  test "request fallback continues uncompressed when compression step raises" do
    parent = self()

    req =
      Req.new(
        body: [123_456],
        compress_body: true,
        adapter: fn req ->
          send(
            parent,
            {:request, Req.Request.fetch_option(req, :compress_body),
             Req.Request.get_header(req, "content-encoding")}
          )

          {req, Req.Response.new(status: 200, body: %{})}
        end
      )

    assert {:ok, %{status: 200}} = Client.request(req, :post, "/", [])
    assert_received {:request, {:ok, false}, []}
  end

  test "request retries keep the compressed body reusable" do
    parent = self()
    {:ok, calls} = Agent.start_link(fn -> 0 end)

    req =
      Req.new(
        base_url: "https://example.com",
        retry: :transient,
        retry_delay: fn _ -> 0 end,
        max_retries: 2,
        compress_body: true,
        adapter: fn req ->
          attempt = Agent.get_and_update(calls, fn calls -> {calls + 1, calls + 1} end)
          send(parent, {:request, Req.Request.get_header(req, "content-encoding"), req.body})
          {req, Req.Response.new(status: if(attempt <= 2, do: 503, else: 200), body: %{})}
        end
      )

    assert {:ok, %{status: 200}} = Client.request(req, :post, "/", json: %{event: "test"})
    assert_received {:request, ["gzip"], first_body}
    assert_received {:request, ["gzip"], second_body}
    assert_received {:request, ["gzip"], third_body}
    assert :zlib.gunzip(first_body) == :zlib.gunzip(second_body)
    assert :zlib.gunzip(second_body) == :zlib.gunzip(third_body)
  end

  test "request fallback does not catch adapter exceptions" do
    {:ok, calls} = Agent.start_link(fn -> 0 end)

    req =
      Req.new(
        body: "ok",
        compress_body: true,
        adapter: fn _req ->
          Agent.update(calls, &(&1 + 1))
          raise RuntimeError, "request failed"
        end
      )

    assert_raise RuntimeError, "request failed", fn ->
      Client.request(req, :post, "/", [])
    end

    assert Agent.get(calls, & &1) == 1
  end

  test "request fallback preserves normal compression" do
    parent = self()

    req =
      Req.new(
        body: "hello",
        compress_body: true,
        adapter: fn req ->
          send(parent, {:request, Req.Request.get_header(req, "content-encoding"), req.body})
          {req, Req.Response.new(status: 200, body: %{})}
        end
      )

    assert {:ok, %{status: 200}} = Client.request(req, :post, "/", [])
    assert_received {:request, ["gzip"], gzipped_body}
    assert :zlib.gunzip(gzipped_body) == "hello"
  end
end
